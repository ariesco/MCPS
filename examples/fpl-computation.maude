fmod SYNTAX is
  pr QID .

  sorts Var Num Op Exp BVar Boolean BOp BExp FunVar VarList NumList ExpList Prog Dec .

***  subsort Qid < Var .
  op V : Qid -> Var .
  subsort Var < Exp .
  subsort Num < Exp .

***  subsort Qid < BVar .
  op BV : Qid -> BVar .
  subsort BVar < BExp .
  subsort Boolean < BExp .

  op FV : Qid -> FunVar .

  ops + - * : -> Op [ctor] .

  op z : -> Num [ctor] .
  op s : Num -> Num [ctor iter] .

  subsort Exp < ExpList .
  op nilEL : -> ExpList [ctor] .
  op _,_ : ExpList ExpList -> ExpList [ctor assoc prec 30] .

  op ___ : Exp Op Exp -> Exp [ctor prec 20] .
  op let_=_in_ : Var Exp Exp -> Exp [ctor prec 25] .
  op If_Then_Else_ : BExp Exp Exp -> Exp [ctor prec 25] .
  op _`(_`) : FunVar ExpList -> Exp [ctor prec 15] .
  *** Msg
  op to_:_ : Qid Exp -> Exp [ctor prec 10] .
  *** Receive
  op receive : -> Exp [ctor] .

  ops T F : -> Boolean [ctor] .
  ops And Or : -> BOp [ctor] .
  op ___ : BExp BOp BExp -> BExp [ctor prec 20] .
  op Not_ : BExp -> BExp [ctor prec 15] .
  op Equal : Exp Exp -> BExp [ctor] .

  subsort Var < VarList .
  op nilVL : -> VarList .
  op _,_ : VarList VarList -> VarList [assoc prec 30] .
  subsort VarList < ExpList .

  subsort Num < NumList .
  op nilNL : -> NumList .
  op _,_ : NumList NumList -> NumList [assoc prec 30] .
  subsort NumList < ExpList .

  op <_,_> : Exp Dec -> Prog .
  op nil : -> Dec .
  op _`(_`)<=_ : FunVar VarList Exp -> Dec [prec 30] .
  op _&_ : Dec Dec -> Dec [assoc comm id: nil prec 40] .

  op exDec1 : -> Dec .
  eq exDec1 =
    FV('Fac)(V('x)) <= If Equal(V('x),z) Then s(z)
                       Else V('x) * FV('Fac)(V('x) - s(z)) &
    FV('Rem)(V('x) , V('y)) <= If Equal(V('x),V('y)) Then z
                             Else If Equal(V('y) - V('x), z) Then V('y)
                                  Else FV('Rem)(V('x) , V('y) - V('x)) &
    FV('Double)(V('x)) <= V('x) + V('x) .
endfm

fmod AP is
  pr SYNTAX .

  op Ap : Op Num Num -> Num .
  op _+._ : Num Num -> Num .
  op _*._ : Num Num -> Num .
  op _-._ : Num Num -> Num .

  vars n n' : Num .

  eq Ap(+,n,n') = n +. n' .
  eq Ap(*,n,n') = n *. n' .
  eq Ap(-,n,n') = n -. n' .
  eq z +. n = n .
  eq s(n) +. n' = s(n +. n') .
  eq z *. n = z .
  eq s(n') *. n = (n' *. n) +. n .
  eq z -. n = z .
  eq s(n') -. z = s(n') .
  eq s(n') -. s(n) = n' -. n .

  op Ap : BOp Boolean Boolean -> Boolean .
  op _and_ : Boolean Boolean -> Boolean .
  op _or_ : Boolean Boolean -> Boolean .

  var bv bv' : Boolean .

  eq Ap(And,bv,bv') = bv and bv' .
  eq Ap(Or,bv,bv') = bv and bv' .
  eq T and bv = bv .
  eq F and bv = F .
  eq T or bv = T .
  eq F or bv = bv .

endfm

fmod ENV is
  inc SYNTAX .

  sorts Value Variable .

  subsorts Num Boolean < Value .
  subsorts Var BVar < Variable .

  sort ENV .

  op mt : -> ENV .
  op _=_ : Variable Value -> ENV [prec 20] .
  op __ : ENV ENV -> ENV [assoc id: mt prec 30] .
  op _`(_`) : ENV Var -> Num .
  op _`(_`) : ENV BVar -> Boolean .
  op _`[_/_`] : ENV Value Variable -> ENV [prec 35] .
  op _`[_/_`] : ENV NumList VarList -> ENV [prec 35] .
  op remove : ENV Variable -> ENV .

  vars X X' : Variable .
  var V : Value .
  var ro : ENV .
  var n : Num .
  var x : Var .
  var nl : NumList .
  var xl : VarList .

  eq remove(mt, X) = mt .
  eq remove(X = V ro, X') = if X == X' then ro else X = V remove(ro,X') fi .
  eq ro [V / X] = remove(ro,X) X = V .
  eq ro [n, nl / x, xl] = (ro[n / x]) [nl / xl] .
  eq ro [nilNL / nilVL] = ro .
  eq (X = V ro)(X') = if X == X' then V else ro(X') fi .

endfm

fmod SUBSTITUTION is
  pr SYNTAX .
  pr CONVERSION .

  sort VarSet .

  op mt : -> VarSet .
  subsort Var < VarSet .
  op _U_ : VarSet VarSet -> VarSet [assoc comm id: mt] .
  eq x U x = x . *** idempotency

  *** FVar returns the free Var (not BVar)
  op FVar : Exp -> VarSet .
  op FVar : BExp -> VarSet .
  op FVar : ExpList -> VarSet .

  op _in_ : Var VarSet -> Bool .
  op _not-in_ : Var VarSet -> Bool .
  op _\_ : VarSet VarSet -> VarSet .
  op new : VarSet -> Var .
  op new : VarSet Nat -> Var .

  var n : Num .
  vars x y x' : Var .
  vars e e' e1 e2 : Exp .
  var op : Op .
  var F : FunVar .
  var bx : BVar .
  vars be be1 be2 : BExp .
  var bop : BOp .
  var el : ExpList .
  vars VS VS' : VarSet .
  var N : Nat .
  var xl : VarList .

  eq FVar(n) = mt .
  eq FVar(x) = x .
  eq FVar(e1 op e2) = FVar(e1) U FVar(e2) .
  eq FVar(If be Then e1 Else e2) = FVar(be) U FVar(e1) U FVar(e2) .
  eq FVar(let x = e in e') = (FVar(e') \ x) U FVar(e) .
  eq FVar(F(el)) = FVar(el) .
  eq FVar(nilEL) = mt .
  eq FVar(e,el) = FVar(e) U FVar(el) .
  eq FVar(T) = mt .
  eq FVar(F) = mt .
  eq FVar(Not be) = FVar(be) .
  eq FVar(be1 bop be2) = FVar(be1) U FVar(be2) .
  eq FVar(bx) = mt .
  eq FVar(Equal(e1,e2)) = FVar(e1) U FVar(e2) .

  eq x in mt = false .
  eq x in (y U VS) = (x == y) or (x in VS) .

  eq x not-in VS = not (x in VS) .

  eq (mt \ VS') = mt .
  eq (y U VS) \ VS' = if (y in VS') then VS \ VS'
                      else y U (VS \ VS') fi .


  eq new(VS) = new(VS, 0) .
***  eq new(VS, N) = if V(index('z,N)) not-in VS then V(index('z,N))
  eq new(VS, N) = if V(qid(string(N, 10))) not-in VS then V(qid(string(N, 10)))
                  else new(VS, N + 1) fi .

  *** substitution of an expression for a variable, page 53

  op _[_/_] : Exp Exp Var -> Exp .
  op _[_/_] : BExp Exp Var -> BExp .
  op _[_/_] : ExpList Exp Var -> ExpList .

  eq y [e' / x] = if x == y then e' else y fi .

  eq n [e' / x] = n .

  eq (e1 op e2) [e' / x] = (e1 [e' / x]) op (e2 [e' / x]) .

  eq (If be Then e1 Else e2) [e' / x] =
      If (be[e' / x]) Then (e1[e' / x]) Else (e2[e' / x]) .

  eq (let x = e1 in e2) [e' / x] = let x = (e1 [e' / x]) in e2 .

  ceq (let y = e1 in e2) [e' / x] =
       let y = (e1 [e' / x]) in (e2 [e' / x])
       if x =/= y /\ y not-in FVar(e') .

  ceq (let y = e1 in e2) [e' / x] =
       let x' = (e1 [e' / x]) in ((e2[x' / y]) [e' / x])
       if x =/= y /\ y in FVar(e') /\
          x' := new(FVar(e') U FVar(e2)) .

  eq F(el) [e' / x] = F(el [e' / x]) .

  eq nilEL [e' / x] = nilEL .
  eq (e, el) [e' / x] =  (e[e' / x]), (el[e' / x]) .

  eq T [e' / x] = T .
  eq F [e' / x] = F .

  eq bx [e' / x] = bx .

  eq (Not be) [e' / x] = Not (be[e' / x]) .

  eq (be1 bop be2) [e' / x] = (be1 [e' / x]) bop (be2 [e' / x]) .

  eq Equal(e1,e2) [e' / x] = Equal(e1[e' / x],e2[e' / x]) .


  *** multiple simultaneous substitution

  op _[_/_] : Exp ExpList VarList -> Exp .

  *** simultaneous substitution is equivalent to a sequence of sequential
  *** substitutions if the substituted variables do not occur in the
  *** substituting expressions

  eq e [e', el / x, xl] = (e [e' / x])[el / xl] .

endfm


mod COMPUTATION is
  inc AP .
  inc ENV .
  inc SUBSTITUTION .

  sort Statement .
  subsorts Num Boolean < Statement .
  op _,_|-_ : Dec ENV Exp -> Statement [prec 40] .
  op _,_|-_ : Dec ENV BExp -> Statement [prec 40] .

  vars D D' : Dec .
  var ro : ENV .
  vars n n' : Num .
  vars x x' x1 x2 y : Var .
  vars bx bx' : BVar .
  var v v' v1 v2 : Num .
  var bv bv' : Boolean .
  var op : Op .
  vars e e' e'' e1 e2 e1' e2' : Exp .
  var el : ExpList .
  vars be be' be'' be1 be2 : BExp .
  var bop : BOp .
  var F : FunVar .

  *** page 103

  rl [VarRc] : D,ro |- x => ro(x) .

  rl  [OpRc] : D,ro |- v op v' => Ap(op,v,v') .
  crl [OpRc] : D,ro |- e op e' => e'' op e'
            if D,ro |- e  => e'' .
  crl [OpRc] : D,ro |- e op e' => e op e''
            if D,ro |- e'  => e'' .

  crl [IfRc] : D,ro |- If be Then e Else e' => If be' Then e Else e'
            if D,ro |- be => be' .
  rl  [IfRc] : D,ro |- If T Then e Else e' => e .
  rl  [IfRc] : D,ro |- If F Then e Else e' => e' .

  crl [LocRc] : D,ro |- let x = e in e' => let x = e'' in e'
             if D,ro |- e  => e'' .
  rl  [LocRc] : D,ro |- let x = v in e' => e'[v / x] .

  crl [FunRc] : D,ro |- F(e1) => F(e1')
             if D,ro |- e1 => e1' .
  crl [FunRc] : D,ro |- F(v1) => e[v1 / x1]
             if F(x1)<= e & D' := D .

  crl [FunRc] : D,ro |- F(e1,e2) => F(e1',e2)
             if D,ro |- e1 => e1' .
  crl [FunRc] : D,ro |- F(e1,e2) => F(e1,e2')
             if D,ro |- e2 => e2' .
  crl [FunRc] : D,ro |- F(v1,v2) => e[v1 / x1][v2 / x2]
             if F(x1,x2)<= e & D' := D .


  *** page 104

  rl [VarRc] : D,ro |- bx => ro(bx) .

  rl  [OpRc] : D,ro |- bv bop bv' => Ap(bop,bv,bv') .
  crl [OpRc] : D,ro |- be bop be' => be'' bop be'
            if D,ro |- be  => be'' .
  crl [OpRc] : D,ro |- be bop be' => be bop be''
            if D,ro |- be'  => be'' .

  crl [NotRc] : D,ro |- Not be => Not be'
             if D,ro |- be => be' .
  rl  [NotRc] : D,ro |- Not T => F .
  rl  [NotRc] : D,ro |- Not F => T .

  crl [EqRc] : D,ro |- Equal(e,e') => Equal(e'',e')
            if D,ro |- e => e'' .
  crl [EqRc] : D,ro |- Equal(e,e') => Equal(e,e'')
            if D,ro |- e' => e'' .
  crl [EqRc] : D,ro |- Equal(v,v') => T
            if v == v' .
  crl [EqRc] : D,ro |- Equal(v,v') => F
            if v =/= v' .


  *** reflexive, transitive closure

  op _,_|=_ : Dec ENV Exp -> Statement [prec 40] .

  rl  [zero] : D,ro |= v => v .  *** no step
  crl [more] : D,ro |= e => v
            if D,ro |- e => e'   *** one step
            /\ D,ro |= e' => v . *** all the rest

endm

fmod PROCESS is
 pr COMPUTATION .

 sort Process .

 op [_|_,_|_] : Qid Env Exp ExpList -> Process [ctor] .

*** Dec
endfm

eof .

rew exDec1, mt |= FV('Fac)(s(s(s(s(s(z)))))) .
rew exDec1, mt |= FV('Rem)(s(s(s(z))), s(s(s(s(s(z)))))) .
